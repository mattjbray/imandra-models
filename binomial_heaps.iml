#redef;;
#max_induct 1;;

module Tree = struct
  type 'a t = { value : 'a; children : 'a t list }

  let value tree = tree.value

  let order tree = List.length tree.children

  let singleton x = { value = x; children = [] }

  let merge (t1 : 'a t) (t2 : 'a t) : 'a t =
    if t1.value <= t2.value then
      { t1 with children = t2 :: t1.children }
    else
      { t2 with children = t1 :: t2.children }

  let rec is_valid' parent_order i children =
    match children with
    | [] -> true
    | child :: children' ->
      order child = parent_order - (i + 1) &&
      is_valid' (order child) 0 child.children &&
      is_valid' parent_order (i + 1) children'
  [@@adm children]

  let is_valid t = is_valid' (order t) 0 t.children

  let rec is_min_heap' parent_value (children : int t list) : bool =
    match children with
    | [] -> true
    | child :: children' ->
      child.value >= parent_value &&
      is_min_heap' child.value child.children &&
      is_min_heap' parent_value children'
  [@@adm children]

  let is_min_heap (tree : int t) : bool =
    is_min_heap' tree.value tree.children

  let rec nodes_at_depth' d children =
    if d <= 0 then List.length children else
    match children with
    | [] -> 0
    | child :: children' ->
      nodes_at_depth' (d-1) child.children + nodes_at_depth' d children'
  [@@adm children]

  let nodes_at_depth d tree = if d <= 0 then 1 else nodes_at_depth' (d-1) tree.children

  let rec height' children =
    match children with
    | [] -> 0
    | child :: children' ->
      max (1 + height' child.children) (height' children')

  let height tree = height' tree.children


  (** Theorem: merging two valid trees results in a valid tree. *)

  lemma is_valid_shift i m children =
    is_valid' (i + List.length children) i children ==>
    is_valid' (i + List.length children + m) (i + m) children
  [@@auto]
  ;;

  lemma is_valid_shift_zero children =
    is_valid' (List.length children) 0 children ==>
    is_valid' (1 + List.length children) 1 children
  [@@auto]
  [@@apply is_valid_shift 0 1 children]
  [@@rw]
  ;;

  theorem merge_trees_valid t1 t2 =
    order t1 = order t2 &&
    is_valid t1 && is_valid t2
    ==>
    is_valid (merge t1 t2)
  [@@auto][@@rw]
  ;;

  theorem merge_trees_preserves_min_heap t1 t2 =
    order t1 = order t2 &&
    is_valid t1 && is_valid t2 &&
    is_min_heap t1 && is_min_heap t2
    ==>
    is_min_heap (merge t1 t2)
  [@@auto][@@rw] ;;
end
;;

instance Tree.(fun t -> is_valid t && order t = 3 && value t > 0)
;;

(** Theorem: a binomial tree of order k has (k d) nodes at depth d. *)

let rec fact n =
  if n <= 1 then 1 else
  n * fact (n - 1)

let binomial_coefficient n k =
  fact n / (fact k * fact (n - k))

(* TODO *)
let nodes_at_depth_bin_coeff tree k d =
  let open Tree in
  is_valid tree && order tree = k ==>
  nodes_at_depth d tree = binomial_coefficient k d

(** Theorem: A binomial tree of order k has height k. **)

(* TODO *)
let is_vaild_height t children =
  t.children = children &&
  is_valid t ==> height t = order t

(** Theorem: A binomial tree of order k has 2^k nodes. *)

(* TODO *)

(** Binomial heaps *)

module Heap = struct
  type 'a t = 'a Tree.t option list

  let rec is_valid' i (h : 'a t) : bool =
    match h with
    | [] -> true
    | Some t :: h' ->
      Tree.order t = i &&
      Tree.is_valid t &&
      Tree.is_min_heap t &&
      is_valid' (i + 1) h'
    | None :: h' -> is_valid' (i + 1) h'
  [@@adm h]

  let is_valid h = is_valid' 0 h

  let insert_tree'_measure i t h =
    Ordinal.of_int ((Tree.order t - i) + List.length h)

  let rec insert_tree' i (t : 'a Tree.t) (h : 'a t) : 'a t =
    (* TODO: this check is required to prove this function terminates. Is there
       some way we could erase the runtime check? *)
    if not (is_valid' i h && Tree.is_valid t) || i > Tree.order t then [] else
    if i = Tree.order t then
      match h with
      | [] -> [ Some t ]
      | None :: h' -> Some t :: h'
      | Some t' :: h' -> None :: insert_tree' (i + 1) (Tree.merge t t') h'
    else
      match h with
      | [] -> None :: insert_tree' (i + 1) t []
      | t' :: h' -> t' :: insert_tree' (i + 1) t h'
  [@@measure insert_tree'_measure i t h]

  let insert_tree t h = insert_tree' 0 t h

  lemma insert_tree_valid_corrolary i t h =
    Tree.is_valid t && Tree.is_min_heap t && is_valid' i h ==>
    is_valid' i (insert_tree' i t h)
  [@@auto][@@rw];;

  theorem insert_tree_valid t h =
    Tree.is_valid t && Tree.is_min_heap t && is_valid h ==>
    is_valid (insert_tree t h)
  [@@auto][@@apply insert_tree_valid_corrolary 0 t h][@@rw];;
end
;;

instance (fun h -> Heap.is_valid 0 h && match h with
| [ Some _; None; _; Some _ ] -> true
| _ -> false);;
